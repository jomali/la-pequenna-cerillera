

!!==============================================================================
!!
!!	LA PEQUEÑA CERILLERA
!!	Definición de localidades y objetos
!!
!!==============================================================================
!!
!!	Archivo:		cerillera_objects.inf
!!	Autor(es):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		3.0
!!	Fecha:			2018/05/11
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte del relato interactivo
!!	LA PEQUEÑA CERILLERA.
!!
!!	LA PEQUEÑA CERILLERA es software libre: usted puede redistribuirlo
!!	y/o modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya sea la
!!	versión 3 de la Licencia, o (a su elección) cualquier versión
!!	posterior.
!!
!!	LA PEQUEÑA CERILLERA se distribuye con la esperanza de que sea
!!	útil, pero SIN GARANTíA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los
!!	detalles de la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU
!!	junto a LA PEQUEÑA CERILLERA. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2010-2018, J. Francisco Martín
!!
!!------------------------------------------------------------------------------
!!
!!	TABLA DE CONTENIDOS
!!
!!	1)	Limbo
!!	2)	Esquina
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	1)	Limbo
!!
!!------------------------------------------------------------------------------

Room	Limbo "En algún lugar"
 with	name_f	'babia' 'parte',
		name_m	'limbo' 'lugar' 'sueno' 'vacio',
		gender	G_MASCULINO,
		adjectives 'algun' 'alguna',
		description [;
			O__M(self, 1);
			if (self hasnt visited) O__M(self, 2);
			new_line;
			return true;
		],
		clarification 0,
 has	male;

Atrezzo	-> RestosCalcinados "restos calcinados"
 with	name_m	'resto',
		name_mp	'restos',
		gender	G_MASCULINO + G_PLURAL,
		adjectives
			'consumido' 'calcinado' 'quemado' 'extinguido' 'ennegrecido'
			'consumidos' 'calcinados' 'quemados' 'extinguidos' 'ennegrecidos'
			'inservible' 'inservibles',
		description [;
			if (Esquina.scene == 2) return O__M(self, 2);
			else return O__M(self, 1);
		],
 has	male pluralname;

NPC		-> Alba "cerillera"
 with	name_f	'cerillera' 'chavala' 'chavalilla' 'chica' 'chiquilla' 'cria'
				'muchacha' 'nina' 'pequena' 'personaje' 'protagonista',
		name_m	'cabello' 'cuello' 'pelo',
		name_mp	'cabellos' 'rizos',
		gender	G_FEMENINO,
		adjectives
			'asustada' 'congelada' 'hambrienta' 'helada' 'hermoso' 'hermosos'
			'pobre' 'principal' 'rubio' 'rubios',
		description [;
			O__M(self, 1);
			new_line;
			<<Inv>>;
		],
		before [
			previous_style;
			DefaultAction:
				previous_style = UseTextStyle(TEXT_STYLE_PARSER);
				print (string) TEXT_STYLE_PARSER_PREFIX;
				print "examina ", (name) self;
				print (string) TEXT_STYLE_PARSER_SUFIX;
				UseTextStyle(previous_style);
				new_line;
				<<Examine self>>;
		],
		final_credits [
			previous_style;
			O__M(self, 8);
			KeyCharPrimitive();
			new_line;
			previous_style = UseTextStyle(TEXT_STYLE_PARSER);
			print (string) TEXT_STYLE_PARSER_PREFIX;
			print "Si quieres volver a empezar el cuento pulsa @<<R@>>.
			Cualquier otra tecla para salir.";
			print (string) TEXT_STYLE_PARSER_SUFIX;
			UseTextStyle(previous_style);
			if (KeyCharPrimitive() == 'r' or 'R') {
				FadeOutTrack(MUS_FINAL, 0, 1000, 0, true);
				@restart;
				L__M(##Restart, 2);
			}
			else quit;
		],
 has	female remarkable;

Item	-> -> CajaCerillas "caja de cerillas"
 with	name_f	'caja' 'cajita',
		name_fp	'cerillas',
		name_m	'estuche' 'fosforo',
		name_mp	'fosforos',
		gender	G_FEMENINO,
		adjectives 'abierta' 'abierto' 'cerrada' 'cerrado',
		invent [;
			if ( inventory_stage == 1 && self hasnt open )
				O__M(self, 3);
			if ( inventory_stage == 1 && self has open )
				O__M(self, 4);
			return true;
		],
		description [;
			SetPronoun('-lo', self);
			O__M(self, 1);
			if (self hasnt open) O__M(self, 2);
			new_line;
			return true;
		],
		before [
			previous_style;
			DefaultAction:
				if (self hasnt open) {
					previous_style = UseTextStyle(TEXT_STYLE_PARSER);
					print (string) TEXT_STYLE_PARSER_PREFIX;
					print "abre ", (name) self;
					print (string) TEXT_STYLE_PARSER_SUFIX;
					UseTextStyle(previous_style);
					new_line;
					<<Open self>>;
				} else if (IndirectlyContains(self, Cerilla)) {
					previous_style = UseTextStyle(TEXT_STYLE_PARSER);
					print (string) TEXT_STYLE_PARSER_PREFIX;
					print "coge ", (name) Cerilla;
					print (string) TEXT_STYLE_PARSER_SUFIX;
					UseTextStyle(previous_style);
					new_line;
					<<Take Cerilla>>;
				}
			Drop, Insert, PutOn, Transfer:
				return O__M(self, 5);
			LetGo:
				if (noun == Cerilla) {
					move Cerilla to player;
					return O__M(self, 6);
 				}
				if (~~IndirectlyContains(self, Cerilla))
					return O__M(self, 7);
		],
		after [;
			Open: if ( ~~self.flag_open ) {
				SetPronoun('-lo', self);
				self.flag_open = true;
				return O__M(self, 8);
			}
		],
		flag_open	false,
 has	female transparent container openable ~open;

Item	-> -> -> Cerilla "cerilla"
 with	name_f	'cerilla',
		name_m	'fosforo' 'palillo' 'palito',
		gender	G_FEMENINO,
		adjectives 'apagada' 'apagado' 'encendida' 'encendido' 'madera',
		description [;
			if (self hasnt light) {
				return O__M(self, 1);
			} else if (self in player) {
				return O__M(self, 2);
			} else {
				return O__M(self, 3);
			}
		],
		before [
			previous_style;
			DefaultAction:
				previous_style = UseTextStyle(TEXT_STYLE_PARSER);
				print (string) TEXT_STYLE_PARSER_PREFIX;
				print "enciende ", (name) self;
				print (string) TEXT_STYLE_PARSER_SUFIX;
				UseTextStyle(previous_style);
				new_line;
				<<Burn self>>;
			Burn:
				if (~~IndirectlyContains(player, self)) {
					return O__M(self, 4);
				}
				if (~~IndirectlyContains(player, CajaCerillas)) {
					return O__M(self, 5);
				}
				self.light_match();
				return true;
			Take:
				if (IndirectlyContains(CajaCerillas, self)
					&& CajaCerillas hasnt open) {
					<Open CajaCerillas>;
					new_line;
				}
				if (IndirectlyContains(player, self)) {
					return O__M(self, 6);
				}
		],
		light_match [;
			if (IndirectlyContains(CajaCerillas, self)
				&& CajaCerillas hasnt open) {
				if (Esquina.scene == 1) return O__M(self, 7);
				give CajaCerillas open;
			}
			switch (Esquina.scene) {
				1:
					Esquina.scene++;
					move RestosCalcinados to Esquina;
					move self to CajaCerillas;
					O__M(self, 8);
					new_line;
					<<Look>>;
				2:
					Esquina.scene++;
					move self to CajaCerillas;
					O__M(self, 9);
					new_line;
					<<Look>>;
				3:
					Esquina.scene++;
					move self to CajaCerillas;
					give location general;
					O__M(self, 10);
					new_line;
					<<Look>>;
				4:
					Esquina.scene++;
					FadeInTrack(MUS_FINAL, -1, 10000);
					move self to CajaCerillas;
					O__M(self, 11);
					KeyCharPrimitive();
					new_line;
					O__M(self, 12);
					KeyCharPrimitive();
					new_line;
					O__M(self, 13);
					KeyCharPrimitive();
					new_line;
					O__M(self, 14);
					KeyCharPrimitive();
					new_line;
					O__M(self, 15);
					KeyCharPrimitive();
					new_line;
					HorizontalRule();
					KeyCharPrimitive();
					new_line;
					return Alba.final_credits();
			}
		],
 has	female;


!!==============================================================================
!!
!!	2)	Esquina
!!
!!------------------------------------------------------------------------------

!!	-	'general' se activa cuando aparece la estrella fugaz en el cielo
Room	Esquina "Acurrucada en una esquina"
 with	name_f	'esquina',
		gender	G_FEMENINO,
		description [;
			switch (self.scene) {
				1:	return O__M(self, 1);
				2:	return O__M(self, 2);
				3:	return O__M(self, 3);
				4:	return O__M(self, 4);
			}
		],
		before [;
			Listen:
				if (noun == nothing)
					return O__M(self, 5);
			Smell:
				if (noun == nothing)
					return O__M(self, 6, (self.scene == 2));
		],
		in_to [;
			<<Open Puerta>>;
		],
		out_to [;
			return self.cant_go();
		],
		cant_go [;
			O__M(self, 7);
			if (self.try_to_go_home == false) {
				self.try_to_go_home = true;
				O__M(self, 8);
			}
			new_line;
			return true;
		],
		scene 1,
		try_to_go_home false,
 has	female;

Object	-> HelpManager "(Help Manager)"
 with	react_before [;
			Burn:
				if (self.hint_burn_b && ~~self.hint_burn_b_shown
					&& noun == Cerilla) {
					self.hint_burn_b_shown = true;
				}
			DefaultAction:
				if (self.hint_default_action
					&& ~~self.hint_default_action_shown) {
					self.hint_default_action_shown = true;
					O__M(self, 10);
					new_line;
				}
			Examine:
				if (self.hint_examine && ~~self.hint_examine_shown) {
					self.hint_examine_shown = true;
					O__M(self, 7);
					new_line;
				}
				if (self.hint_inventory && ~~self.hint_inventory_shown
					&& noun == Alba) {
					self.hint_inventory_shown = true;
				}
			Inv:
				if (self.hint_inventory && ~~self.hint_inventory_shown) {
					self.hint_inventory_shown = true;
				}
			Look:
				if (self.hint_look && ~~self.hint_look_shown) {
					self.hint_look_shown = true;
					O__M(self, 5);
					new_line;
				}
			Open:
				if (self.hint_open && ~~self.hint_open_shown
					&& noun == CajaCerillas) {
					self.hint_open_shown = true;
					O__M(self, 14);
					new_line;
				}
			Use:
				return O__M(self, 12);
		],
		react_after [;
			if (self.initial_hint) {
				self.initial_hint = false;
				new_line;
				return O__M(self, 1);
			}
		],
		new_hint [;
			if (~~self.hint_look_shown) {
				self.hint_look = true;
				return O__M(self, 4);
			} else if (~~self.hint_examine_shown) {
				self.hint_examine = true;
				return O__M(self, 6);
			} else if (~~self.hint_default_action_shown) {
				self.hint_default_action = true;
				#Ifdef TARGET_ZCODE;
				return O__M(self, 8);
				#Ifnot; ! TARGET_GLULX;
				return O__M(self, 9);
				#Endif; ! TARGET_
			} else if (~~self.hint_inventory_shown) {
				self.hint_inventory = true;
				return O__M(self, 11);
			} else if (~~self.hint_open_shown) {
				self.hint_open = true;
				return O__M(self, 13);
			} else if (~~self.hint_burn_a_shown) {
				self.hint_burn_a_shown = true;
				return O__M(self, 15);
			} else if (~~self.hint_burn_b_shown) {
				self.hint_burn_b = true;
				return O__M(self, 16);
			} else if (~~self.hint_highlight_shown) {
				self.hint_highlight_shown = true;
				return O__M(self, 17);
			} else if (~~self.hint_meta_commands_shown) {
				self.hint_meta_commands_shown = true;
				return O__M(self, 18);
			} else {
				#Ifdef TARGET_ZCODE;
				return O__M(self, 2);
				#Ifnot; ! TARGET_GLULX;
				return O__M(self, 3);
				#Endif; ! TARGET_
			}
		],
 private
		hint_burn_a_shown false,
		hint_burn_b false,
		hint_burn_b_shown false,
		hint_default_action false,
		hint_default_action_shown false,
		hint_examine false,
		hint_examine_shown false,
		hint_highlight_shown false,
		hint_inventory false,
		hint_inventory_shown false,
		hint_look false,
		hint_look_shown false,
		hint_meta_commands_shown false,
		hint_open false,
		hint_open_shown false,
		initial_hint true,
 has	scenery;

Atrezzo	-> Adoquines "adoquines"
 with	name_m	'adoquin' 'manto' 'pavimento' 'suelo',
		name_mp	'adoquines',
		name_f	'capa',
		gender	G_MASCULINO + G_PLURAL,
		adjectives
			'nieve' 'blanca' 'blanco' 'helado' 'helada' 'gelido'
			'gelida' 'frio' 'fria',
		description [; return O__M(self, 1); ],
		react_before [; Examine: if (noun == d_obj) <<Examine self>>; ],
 has	male pluralname;

Atrezzo	-> Calle "calle"
 with	name_m	'paseo' 'frio',
		name_f	'calle' 'via',
		name_fp	'tinieblas',
		gender	G_FEMENINO,
		adjectives 'adoquinado' 'adoquinada',
		description [; return O__M(self, 1); ],
		before [;
			Listen:	<<Listen>>;
			Smell:	<<Smell>>;
		],
 has	female;

Atrezzo	-> Casas "casas"
 with	name_m	'edificio',
		name_mp	'edificios',
		name_f	'casa' 'ventana',
		name_fp	'casas' 'ventanas',
		gender	G_FEMENINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	female pluralname;

Atrezzo	-> Cielo "cielo"
 with	name_m	'cielo' 'firmamento',
		name_f	'estrella' 'nube',
		name_fp	'estrellas' 'nubes',
		gender	G_MASCULINO,
		adjectives
			'rutilante' 'rutilantes' 'lejana' 'lejanas' 'titilante'
			'titilantes' 'tinta' 'jiron' 'jirones',
		description [; return O__M(self, 1); ],
		react_before [; Examine: if ( noun == u_obj) <<Examine self>>; ],
 has	male;

Atrezzo	-> Faroles "faroles"
 with	name_f	'farola' 'lucecilla' 'lucecita' 'noche' 'oscuridad' 'sombra'
				'tiniebla',
 		name_fp	'farolas' 'lucecillas' 'lucecitas' 'sombras' 'tinieblas',
		name_m	'farol' 'luz',
		name_mp	'faroles' 'luces',
		gender	G_MASCULINO + G_PLURAL,
		adjectives 'debil' 'debiles' 'temblorosa' 'temblorosas',
		description [; return O__M(self, 1); ],
 has	male pluralname;

Atrezzo	-> Nieve "nieve"
 with	name_m	'copo' 'nevazo',
		name_mp	'copos',
		name_f	'nieve' 'nevada' 'nevisca' 'cellisca',
		gender	G_FEMENINO,
		adjectives
			'blanco' 'blanca' 'frio' 'fria' 'helado' 'helada' 'gelido' 'gelida',
		description [; return O__M(self, 1); ],
 has	female;

Atrezzo	-> Pared "pared"
 with	name_m	'muro',
		muro_mp	'muros',
		name_f	'pared',
		name_fp	'paredes',
		gender	G_FEMENINO,
		adjectives
			'piedra' 'frio' 'frios' 'fria' 'frias' 'incomodo'
			'incomodos' 'incomoda' 'incomodas',
		description [; return O__M(self, 1); ],
		before [; Touch: return self.description(); ],
 has	female;

Atrezzo	-> Puerta "puerta"
 with	name_m	'porton',
		name_f	'puerta' 'cancela' 'entrada',
		name_mp	'portones',
		name_fp	'puertas' 'cancelas' 'entradas',
		gander	G_FEMENINO,
		adjectives
			'madera' 'tallada' 'talladas' 'oscura' 'oscuras' 'grande'
			'grandes' 'bonita' 'bonitas',
		description [; O__M(self, 1); new_line; return true; ],
		before [
			previous_style;
			DefaultAction:
				previous_style = UseTextStyle(TEXT_STYLE_PARSER);
				print (string) TEXT_STYLE_PARSER_PREFIX;
				print "llama a la ", (name) self;
				print (string) TEXT_STYLE_PARSER_SUFIX;
				UseTextStyle(previous_style);
				new_line;
				<<KnockOn self>>;
			Pull, Push, PushDir, Turn:
				return O__M(self, 2);
			KnockOn, Open:
				if (self hasnt described) { O__M(self, 1); print " "; }
				return O__M(self, 3);
		],
 has	female door transparent;

Atrezzo	-> -> Aldabas "aldabas"
 with	name_m	'aldabon' 'llamador' 'picaporte',
		name_f	'aldaba',
		name_mp	'aldabones' 'llamadores' 'picaportes',
		name_fp	'aldabas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives
			'metal' 'metalica' 'metalicas' 'metalico' 'metalicos' 'cara'
			'caras' 'de' 'leon' 'leones' 'aguila' 'aguilas' 'oso' 'osos',
		before [;
			DefaultAction:
				<<DefaultAction Puerta>>;
			Examine, Look, Search:
				<<Examine Puerta>>;
			Pull:
				<<Open Puerta>>;
		],
 has	female pluralname;
